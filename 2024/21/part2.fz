dec21 is

  input := io.stdin.read_lines.filter !=""

  # overlapping both layouts, put ` ` at (0,0)
  codepoint.x => "741  <8520^v963AA>"  .find codepoint.this .val / 6
  codepoint.y => "789#456#123# 0^A<v>#".find codepoint.this .val / 4 - 3

  move(cur, next) =>
    dx := next.x - cur.x
    dy := next.y - cur.y
    ve := ["^"," ","v"][dy.sign+1] * (|dy|)
    ho := ["<"," ",">"][dx.sign+1] * (|dx|)

    # I did not believe both paths make a difference for small keypad,
    # convinced by (or stole solution from) https://aoc.csokavar.hu/2024/21/
    p1 := (next.y != 0 || cur.x != 0).as_option String ve+ho+"A"
    p2 := (next.x != 0 || cur.y != 0).as_option String ho+ve+"A"
    p1 ++ p2

  find_shortest(d, l) i64 : memoize => keep "$d$l" _->
    if d < 0 then
      l.byte_length.as_i64
    else
      for res := i64 0, res + s
          cur := "A", next
          next in l.as_codepoints
          s := move cur next .map (find_shortest d-1)
                             .fold1 min
      else
        res

  shortest(n) =>
    lengths := input.map (find_shortest n)
    numeric_ := input.map (.as_codepoints.filter (c -> "0" <= c <= "9") .reduce "" (+) .parse_i64.val)
    lengths.zip numeric_ (*)
           .sum

  part1 => shortest 2
  part2 => shortest 25

  say "$part1:$part2"
