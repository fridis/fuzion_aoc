dec19 =>

  input := io.stdin.read_lines
  input1 := input[0].split ", " .as_array
  input2 := input.drop 2 .filter !=""

  String.suffix(prfx) => substring prfx.byte_length

  M : mutate is
  M ! ()->
    cachePos := (container.mutable_tree_map M String bool).empty
    cacheCnt := (container.mutable_tree_map M String i64 ).empty

    possible(s) =>
      match cachePos.get s
        v bool => v
        nil    => res := s="" || !input1.filter s.starts_with
                                        .map s.suffix
                                        .filter possible
                                        .is_empty
                  cachePos.put s res
                  res

    count(s) =>
      match cacheCnt.get s
        v i64 => v
        nil   => res := s="" ? 1 : input1.filter s.starts_with
                                         .map s.suffix
                                         .map count |> sum
                 cacheCnt.put s res
                 res

    part1 => input2.filter possible .count
    part2 => input2.map count |> sum

    say "$part1:$part2"
