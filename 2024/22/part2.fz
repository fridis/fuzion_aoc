dec22 is

  input := io.stdin.read_lines.filter !="" .map (.parse_i64.val) .as_array

  next(s0 i64) =>
    s1 := s0 * 64
    s2 := s0 ^ s1
    s3 := s2 % 16777216
    s4 := s3 / 32
    s5 := s3 ^ s4
    s6 := s5 % 16777216
    s7 := s6 * 2048
    s8 := s6 ^ s7
    s8  % 16777216

  secrets(x) => x :: next

  prices(seed) : memoize => keep seed _->
    secrets seed .map %10 .take 2001 .as_array
  changes(seed) : memoize => keep seed _->
    prices seed .map (-) .map_pairs (-) .as_array

  part1 =>
    input.map (seed -> secrets seed .drop 2000 .first.val) |> sum

  part2 =>
    mx := mut (i64 0)
    at := mut "--"
    range => i64 -9 .. 9
    N := input.count
    cs := array N (i -> changes input[i])
    for a in range do
      a_idx := array N i->(cs[i] .indexed .filter (t -> t ||> j,v -> v=a && j<1997) .map (t -> t ||> j,v->j) .as_array)
      for b in range do
        b_idx := array N (i -> a_idx[i].filter j->cs[i][j+1]=b .as_array)
        for c in range do
          c_idx := array N (i -> b_idx[i].filter j->cs[i][j+2]=c .as_array)
          for d in range do
            p := (c_idx.indices.flat_map i64 i->
                        c_idx[i].filter j->cs[i][j+3]=d
                                .map (j -> (prices input[i]).drop j+4 .first.get 0) .first) |> sum

            if p > mx.get then
              mx <- p
              at <- "$a $b $c $d"
    mx.get


  say "$part1:$part2"
