dec11 =>

  input := io.stdin.read_string.trim.split "\n" .map (.split ": " .as_tuple)
  nums := container.hash_map (input.map (.0)) 0..input.count-1
  num(name) => nums[name].or_else -1
  succs := input.map (.1.split " " .map dec11.this.num) .as_array

  dac := num "dac"
  fft := num "fft"

  cnts(n,d,f,b u64) is  # counts (n)one, (d)ac, (f)ft, (b)oth

  count(n i32) cnts : memoize => keep n _->
    if n < 0 then cnts 1 0 0 0
    else
      r := succs[n].map count
                   .fold1 a,b->(cnts a.n+b.n a.d+b.d a.f+b.f a.b+b.b)
      if      n=dac then cnts 0 r.n+r.d 0       r.d+r.f+r.b
      else if n=fft then cnts 0 0       r.n+r.f r.d+r.f+r.b
                    else r

  part1 => count (num "you") .n
  part2 => count (num "svr") .b

  say "$part1:$part2"
