dec2 =>

  input := io.stdin.read_string
             .split "\n"
             .first.val
             .split ","
             .flat_map (.split "-"
                        .map (.parse_i64.val)
                        .as_tuples)

  ten := i64 10
  i64.strlen => ($val).byte_length.as_i64

  part1 =>
    input.flat_map ||>a,b->
        sl := (a.strlen+1) / 2
        f := ten**sl
        (max f/10 a/f ..
         min f-1  b/f    ).map hi->
            v := hi * (f+1)
            if a <= v <= b then v
                           else 0
      .sum

  part2 =>
    input.flat_map ||>a,b->
        (i64 2 .. b.strlen).flat_map sn->         # eg.       4
          sl := (a.strlen+sn-1) / sn              #           2
          f0 := ten**sl                           #         100
          f1 := f0**(sn-1)                        #     1000000
          ff := (f1 :: (/f0)).take_while >0 .sum  #     1010101
          (max f0/10 a/f1 ..
           min f0-1  b/f1    ).map hi->
            v := hi * ff                          #    34343434
            if a <= v <= b then v
                           else 0
      .unique
      .sum

  say "$part1:$part2"
