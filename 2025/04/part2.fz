dec4 =>

  input := io.stdin.read_string.trim.split "\n"
             .map (l -> l.codepoints.map ="@" .as_array)
             .as_array

  w := input[0].length
  h := input.length
  a := array2 w+2 h+2 (x,y->is_roll x-1 y-1)

  is_roll(x,y) =>
    0 <= y < h && 0 <= x < w && input[y][x]

  # array2 of bool with directly inaccessible rolls in array2 `o`
  inaccessible1(o) =>
   array2 w+2 h+2 x,y->
    if o[x,y] then ((x-1 .. x+1).pairs (y-1 .. y+1)
                                .count ||>xx,yy->o[xx,yy])>4
              else false

  # count of recursively inaccessible rolls
  inaccessible(o) =>
    i := inaccessible1 o
    co := o.count id
    ci := i.count id
    co = ci ? ci : inaccessible i

  part1 =>
    i := inaccessible1 a
    a.index_pairs.count ||>x,y->
       a[x,y] && !i[x,y]

  part2 =>
    a.count id - inaccessible a

  say "$part1:$part2"
