dec8 =>

  input := io.stdin.read_string.trim.split "\n"
             .map (.split "," .map (.parse_i64.val) .as_array) .as_array
  n := input.length
  m := envir.args[1].parse_i32.val

  dists := (0..n-1).flat_map (i -> (i+1..n-1).map (dist i))
                   .sort

  class(cl, i) =>
    if cl[i]<0 then class cl i+cl[i]
               else i

  dist(i,j) : property.orderable is

    d := input[i].zip input[j] (-) .map **2 .sum

    public redef type.lteq(a,b dist.this) bool => a.d <= b.d

    connect(cl) =>
      ij := (class cl i, class cl j)
      i0 := ij ||> min
      j0 := ij ||> max
      array n x->
        if      i0 = j0         then cl[x]
        else if x  = i0 && x!=j0 then cl[i0] + cl[j0]
        else if class cl x = j0 then i0-x
        else                    cl[x]

  part1 =>
    join(c,cl) i32 =>
      if c = m
        cl.sort.reverse.take 3
          .product
      else
        join c+1 (dists[c].connect cl)

    classes := array n _->1
    join 0 classes

  part2 =>
    join(c,cl) =>
      d := dists[c]
      cl0 := d.connect cl
      if cl0[0]=n then
        input[d.i][0]*input[d.j][0]
      else
        join c+1 cl0

    classes := array n _->1
    join 0 classes

  say "$part1:$part2"
