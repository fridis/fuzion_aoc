dec3_part2 is

  codepoint.is_symbol => !is_ascii_digit && (codepoint val) != "."

  LM : mutate is
  lines := LM ! ()->io.stdin.reader LM ! ()->io.buffered LM .read_lines .filter !=""

  scheme(ls) is
    cp := ls.map x->x.codepoints
    a := array2 cp.count cp[0].count i,j->cp[i][j]
    w => a.length1
    h => a.length0
    index [](l,c) =>
      if 0 <= l < h && 0 <= c < w then a[_,l,c] else "."

    number(l, c i32) is
      len := for res := 1, res+1 while scheme.this[l,c+res].is_ascii_digit else res
      ok := (-1..1) ∃ y -> (-1..len) ∃ x -> scheme.this[l+y,c+x].is_symbol
      val := (0..len-1).map i->scheme.this[l,c+i].as_string
                       .fold String.type.concat
                       .parse_i32
                       .val
      adj (i,j i32) => (l-1 <= i <= l+1   &&
                        c-1 <= j <= c+len   )

    numbers =>
      for res list number := nil, (res ++ f).as_list
          i in a.indices0
      do # NYI: BUG: fails without the `do`, see #4528
          f := for found list number := nil, (found ++ new).as_list  # NYI: PR #2291: `.as_list` not needed if option is a Sequence
                   j in a.indices1
                   p := scheme.this[i,j-1]
                   c := scheme.this[i,j  ]
                   new option number := if !p.is_ascii_digit && c.is_ascii_digit  # NYI: #2292: some syntax sugar `bool.as_option(v Lazy T)` might help
                                          number i j
                                        else
                                          nil
               else found
      else res

    parts =>
      numbers.filter x->x.ok
             .map x->x.val
             .fold i32.type.sum

    gears =>
      lm : mutate is
      lm ! ()->
        res := lm.env.new 0
        for i in a.indices0 do
          for j in a.indices1 do
            if scheme.this[i,j] = "*"
              k := numbers.filter (x->x.adj i j)
              if k.count = 2
                res <- res.get + k.map x->x.val
                                  .fold i32.type.product
        res.get

  s := scheme lines
  p := s.parts
  g := s.gears
  say "$p $g"
