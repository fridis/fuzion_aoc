dec18_part2 is

  input := (io.stdin.with ()->io.buffered.read_lines).val.filter !=""

  data := input.map action
  action(s String) =>
    s3 := s.split " "
    h := (s3[2].substring 2 8).parse_i32_hex.val
    d2 := "RDLU".substring (h % 16) (h % 16)+1
    l2 := h / 16
    action s3[0] s3[1].parse_i32.val d2 l2
  action(dir String, len i32, dir2 String, len2 i32) is

  dirs := "UDLR"
  dx := [0,0,-1,+1]
  dy := [-1,+1,0,0]

  part1 =>
    lm : mutate.
    lm.go i32 ()->
      cell(n lm.new String) ref is
      area := array2 cell 1000 1000 _,_->(cell (lm.env.new "."))
      for px := 500, nx
          py := 500, ny
          min_x := px-1, min min_x px-1
          min_y := py-1, min min_y py-1
          max_x := px+1, max max_x px+1
          max_y := py+1, max max_y py+1
          a in data
          d := (dirs.find a.dir).get
      do
        for i in 0..(a.len-1) do
          area[px+i*dx[d],py+i*dy[d]].n <- "#"
        nx := px + a.len*dx[d]
        ny := py + a.len*dy[d]
      else
        fill(x,y i32) unit =>
          if (min_x <= x <= max_x &&
              min_y <= y <= max_y) then
            if area[x,y].n.get = "." then
              area[x,y].n <- "_"
              fill x-1 y
              fill x+1 y
              fill x y-1
              fill x y+1
        fill min_x min_y
        res := lm.env.new 0 # (max_x-min_x+1)*(max_y-min_y+1)
        for y in min_y..max_y do
          for x in min_x..max_x do
            c := area[x,y].n.get
            if c = "#" || c = "." then
              res <- res.get + 1
        res.get

  part2 =>
    lm : mutate.
    lm.go i64 ()->
      cell(n lm.new String) ref is
      icell(n lm.new i32) ref is
      size => 2000
      area := array2 cell 2*size 2*size _,_->(cell (lm.env.new "."))
      for p0x := 1, nx
          p0y := 1, ny
          ws Sequence i64 := [i64.type.max,i64.type.max], nws.as_array
          hs Sequence i64 := [i64.type.max,i64.type.max], nhs.as_array
          a in data
      do
        l := a.len2.as_i64
        (nx, nws) := if a.dir2 = "R" then
                       for al := l, al-ws[ax]
                           ax := p0x, ax+1
                       while al >= ws[ax] else
                         wl := ws.take ax
                         wm := ws[ax]
                         wr := ws.drop ax+1
                         if al = 0 then
                           if wm-1 = 0 then
                             (ax, ws)
                           else
                             wn Sequence i64 := wl ++ [1, wm-1] ++ wr
                             (ax, wn)
                         else
                           wn Sequence i64 := wl ++ [al-1,1, wm-al] ++ wr
                           (ax+2, wn)
                     else if a.dir2 = "L" then
                       for al := l, al-ws[ax-1]
                           ax := p0x, ax-1
                       while al >= ws[ax-1] else
                         wl := ws.take ax-1
                         wm := ws[ax-1]
                         wr := ws.drop ax
                         if al = 0 then
                           if wm-1=0
                             (ax, ws)
                           else
                             wn Sequence i64 := wl ++ [wm-1, 1] ++ wr
                             (ax, wn)
                         else
                           wn Sequence i64 := wl ++ [wm-al-1, 1, al] ++ wr
                           (ax+1, wn)
                     else
                       (p0x, ws)
        (ny, nhs) := if a.dir2 = "D" then
                       for al := l, al-hs[ay]
                           ay := p0y, ay+1
                       while al >= hs[ay] else
                         wl := hs.take ay
                         wm := hs[ay]
                         wr := hs.drop ay+1
                         if al = 0 then
                           if wm-1 = 0 then
                             (ay, hs)
                           else
                             wn Sequence i64 := wl ++ [1, wm-1] ++ wr
                             (ay, wn)
                         else
                           if (wm-al-1=0) panic "wm-al-1 is 0 A"
                           wn Sequence i64 := wl ++ [al-1, 1, wm-al] ++ wr
                           (ay+2, wn)
                     else if a.dir2 = "U" then
                       for al := l, al-hs[ay-1]
                           ay := p0y, ay-1
                       while al >= hs[ay-1] else
                         wl := hs.take ay-1
                         wm := hs[ay-1]
                         wr := hs.drop ay
                         if al = 0 then
                           if wm-1=0
                             (ay, hs)
                           else
                             wn Sequence i64 := wl ++ [wm-1, 1] ++ wr
                             (ay, wn)
                         else
                           if (wm-al-1=0) panic "wm-al-1 is 0"
                           wn Sequence i64 := wl ++ [wm-al-1, 1, al] ++ wr
                           (ay+1, wn)
                     else
                       (p0y, hs)
      else
        for p1x := p0x, nx
            p1y := p0y, ny
            px := p1x + size
            py := p1y + size
            min_x := px-1, min min_x px-1
            min_y := py-1, min min_y py-1
            max_x := px+1, max max_x px+1
            max_y := py+1, max max_y py+1
            a in data
            d := a.dir2
            l := a.len2.as_i64
        do
          nx := if d = "R" then
                  for al := l, al - ws[pnx]
                      pnx := p1x, pnx+1
                  while al > 0 do
                    area[pnx+size,p1y+size].n <- "#"
                  else
                    pnx
                else if d = "L" then
                  for al := l, al - ws[pnx-1]
                      pnx := p1x, pnx-1
                  while al > 0 do
                    area[pnx+size,p1y+size].n <- "#"
                  else
                    pnx
                else
                  p1x
          ny := if d = "D" then
                  for al := l, al - hs[pny]
                      pny := p1y, pny+1
                  while al > 0 do
                    area[p1x+size,pny+size].n <- "#"
                  else
                    pny
                else if d = "U" then
                  for al := l, al - hs[pny-1]
                      pny := p1y, pny-1
                  while al > 0 do
                    area[p1x+size,pny+size].n <- "#"
                    pny
                else
                  p1y
        else
          fill(x,y i32) unit =>
            if (min_x <= x <= max_x &&
                min_y <= y <= max_y) then
              if area[x,y].n.get = "." then
                area[x,y].n <- "_"
                fill x-1 y
                fill x+1 y
                fill x y-1
                fill x y+1
          fill min_x min_y
          (min_y..max_y).reduce (i64 0) s1,y->
            (min_x..max_x).reduce s1 s2,x->
              c := area[x,y].n.get
              if c = "#" || c = "." then
                ww := ws[x-size]
                hh := hs[y-size]
                s2 + ww*hh
              else
                s2

  say "part1 $part1 part2 $part2"
