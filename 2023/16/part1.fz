dec16_part1 is

  LM : mutate is
  input := LM ! ()->io.stdin.reader LM ! ()->io.buffered LM
    .read_lines
    .filter !=""
    .map (.codepoints)
  area := array2 String input.count input[0].count i,j->input[i][j]
  xs => area.indices1
  ys => area.indices0

  r => {0}; l => {1}; u => {2}; d => {3}
  part1 =>
    lm : mutate is
    lm ! ()->
      visited := (lm.array i32).type.new area.length.as_i64 0
      ix(x,y) => y+x*area.length1
      trace(x,y,w i32) unit =>
        if ((x ∈ xs) && (y ∈ ys) &&
            (visited[ix x y .as_i64] & (1 << w)) = 0) then
          visited[ix x y .as_i64] := visited[ix x y .as_i64] | (1 << w)
          c := area[y,x]
          if (["\\|", "/|" , "xx"  , ".|" ][w].find c).exists then trace x   y+1 d
          if (["/|" , "\\|", ".|"  , "xx" ][w].find c).exists then trace x   y-1 u
          if (["xx" , ".-" , "\\-" , "/-" ][w].find c).exists then trace x-1 y   l
          if ([".-" , "xx" , "/-"  , "\\-"][w].find c).exists then trace x+1 y   r
      trace 0 0 r
      (visited.as_array.filter !=0).count

  say "part1 $part1"
