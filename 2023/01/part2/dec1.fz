dec1 is

  num_to_digits_table := [
    ("one"  , "1"),
    ("two"  , "2"),
    ("three", "3"),
    ("four" , "4"),
    ("five" , "5"),
    ("six"  , "6"),
    ("seven", "7"),
    ("eight", "8"),
    ("nine" , "9"),
    ]

  num_to_digit(s String) =>
    s1 := for m in num_to_digits_table
    until s.starts_with m.values.0
      s.replace m.values.0 m.values.1 1
    else
      s
    if s1 != ""
      f := s1.substring 0 1
      t := s.substring 1  # this should IMHO be s1.substring to avoid counting overlaps twice as in "fiveight", but ok.
      r := f + num_to_digit t
      # (s.substring 0 1) + num_to_digits (s.sub_string 1)
      r
    else
      s

  r := io.stdin.with ()->
    for
      sum := 0, sum + 10 * first + last
      l := io.buffered.read_line
    while l != "" do
      d := num_to_digit l
      digits := d.as_codepoints
                 .filter x->(codepoint.type.ascii_digit.contains x.val)
      first := digits.first.parse_i32.val
      last  := digits.last .parse_i32.val
    else
      sum

  say r
